import uk.gov.hmcts.rse.AuthMode
import uk.gov.hmcts.rse.CftlibExec

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      name = "AzureArtifacts"
      url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
      content {
        excludeGroup "com.github.ben-manes.versions"
      }
    }

  }
  dependencies {
    classpath group: 'cz.habarta.typescript-generator', name: 'typescript-generator-gradle-plugin', version: '3.2.1263'
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'idea'
  id 'pmd'
  id 'jacoco'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'org.sonarqube' version '6.3.1.5724'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.10'
  id 'com.github.ben-manes.versions' version '0.50.0'
  id 'hmcts.ccd.sdk' version '6.6.0'
  id "au.com.dius.pact" version "4.3.10"
  id 'com.github.hmcts.rse-cft-lib' version '0.19.2017'
}

apply plugin: 'cz.habarta.typescript-generator'
apply plugin: 'com.github.ben-manes.versions'

group = 'uk.gov.hmcts'
version = '0.0.1'

ccd {
  configDir = file('ccd-definitions/definitions')
  decentralised = true
  caseEventServiceBus = true
}

project.tasks.named("generateCCDConfig") {
  environment 'SPRING_PROFILES_ACTIVE', 'config-gen'
  environment 'SPRING_FLYWAY_ENABLED', 'false'
  environment 'SPRING_JMS_SERVICEBUS_ENABLED', 'false'
  environment 'SPRING_DATASOURCE_URL', 'jdbc:h2:mem:testdb;DB_CLOSE_DELAY=-1;MODE=PostgreSQL'
  environment 'SPRING_DATASOURCE_USERNAME', 'sa'
  environment 'SPRING_DATASOURCE_PASSWORD', ''
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
  }
  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
  }
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/contractTest/java')
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

ext{
  set('snakeyaml.version', '2.0')
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

idea {
  module {
    testSourceDirs += file('src/integrationTest/java')
    testSourceDirs += file('src/functionalTest/java')
  }
}

ext['elasticsearch.version'] = '7.16.3'

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntime.extendsFrom testRuntime
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntime
  cftlibTestImplementation.extendsFrom testImplementation
  cftlibTestRuntime.extendsFrom testRuntime
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()
  maxParallelForks = Math.floorDiv(Runtime.runtime.availableProcessors(), 2)
  //Use more available processors for FT as WA Tests have timeouts and will run out of time
  if (System.getenv('WA_FUNCTIONAL_TESTS_ENABLED') == 'true') {
    maxParallelForks = Runtime.runtime.availableProcessors()
  }

  testLogging {
    exceptionFormat = 'full'
    events "PASSED", "FAILED", "SKIPPED"
  }
}

test {
  failFast = true
  useJUnitPlatform()
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED','--add-opens=java.base/java.util=ALL-UNNAMED']
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens=java.base/java.util=ALL-UNNAMED']
  environment 'SPRING_PROFILES_ACTIVE', 'functional'
  include "**/*FT.class"
  exclude "uk/gov/hmcts/sptribs/smoke/**"
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
  environment 'SPRING_PROFILES_ACTIVE', 'integration'
  systemProperty 'spring.profiles.active', 'integration'
  include "**/*IT.class"
}

// Invoked from CftLibConfig when booting with CCD.
tasks.register('buildCCDXlsx', Exec) {
  group 'ccd tasks'
  commandLine './bin/ccd-build-definition.sh'
  inputs.dir layout.buildDirectory.dir('definitions')
  outputs.dir layout.buildDirectory.dir('ccd-config')
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
  environment 'SPRING_PROFILES_ACTIVE', 'functional'
  include "uk/gov/hmcts/sptribs/smoke/**"
}

tasks.register('highLevelDataSetup', JavaExec) {
  mainClass.set("uk.gov.hmcts.sptribs.common.ccd.HighLevelDataSetupApp")
  classpath += sourceSets.main.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED', '--add-opens=java.base/java.util=ALL-UNNAMED']
}

tasks.register('fortifyScan', JavaExec) {
  mainClass.set("uk.gov.hmcts.fortifyclient.FortifyClientMainApp")
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

checkstyle {
  maxWarnings = 0
  toolVersion = '9.3'
  getConfigDirectory().set(project.layout.projectDirectory.dir('config/checkstyle').asFile)
}

pmd {
  toolVersion = "7.18.0"
  sourceSets = [sourceSets.main]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files(project.layout.projectDirectory.file('config/pmd/ruleset.xml'))
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'
  outputDirectory = 'build/reports/'
  scanConfigurations = ['runtimeClasspath']

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

if (System.getenv("CI") == null) {
  check.dependsOn integration
  integration.shouldRunAfter(test)
}
project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = [
  '**/uk/gov/hmcts/sptribs/common/ccd/**',
  '**/uk/gov/hmcts/sptribs/common/model/Status.java',
  '**/uk/gov/hmcts/sptribs/ciccase/model/accessprofile/**',
  '**/uk/gov/hmcts/sptribs/caseworker/event/CaseWorkerDraftOrder.java',
  '**/uk/gov/hmcts/sptribs/caseworker/event/CaseworkerCaseFlag.java',
  '**/uk/gov/hmcts/sptribs/common/event/CreateTestCase.java',
  '**/uk/gov/hmcts/sptribs/CaseApiApplication.java',
  '**/uk/gov/hmcts/sptribs/notification/GetAllTemplatesFromNotify.java',
  '**/uk/gov/hmcts/sptribs/ciccase/model/UserRolesForAccessProfiles.java',
  '**/uk/gov/hmcts/sptribs/common/config/**',
  '**/uk/gov/hmcts/sptribs/common/event/UpdateCaseDataEvent.java',
  '**/uk/gov/hmcts/sptribs/common/service/CaseDataFieldService.java'
]

sonarqube {
  properties {
    property "sonar.projectName", "sptribs :: sptribs-case-api"
    property "sonar.projectKey", "uk.gov.hmcts.reform:sptribs-case-api"
    property "sonar.coverage.jacoco.xmlReportPaths", "${projectDir}/build/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}


// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

dependencyManagement {
  dependencies {
    dependencySet(group: 'com.google.guava', version: '33.4.8-jre') {
      entry 'guava'
    }

    //CVE-2022-31692, CVE-2022-31690
    dependencySet(group: 'org.springframework.security', version: '6.5.4'){
      entry 'spring-security-core'
      entry 'spring-security-acl'
      entry 'spring-security-aspects'
      entry 'spring-security-cas'
      entry 'spring-security-config'
      entry 'spring-security-crypto'
      entry 'spring-security-data'
      entry 'spring-security-ldap'
      entry 'spring-security-messaging'
      entry 'spring-security-oauth2-client'
      entry 'spring-security-oauth2-core'
      entry 'spring-security-oauth2-jose'
      entry 'spring-security-oauth2-resource-server'
      entry 'spring-security-openid'
      entry 'spring-security-remoting'
      entry 'spring-security-rsocket'
      entry 'spring-security-saml2-service-provider'
      entry 'spring-security-taglibs'
      entry 'spring-security-test'
      entry 'spring-security-web'
    }

    //Override spring-security-rsa version and update org.bouncycastle:bcpkix-jdk18on for CVE-2023-33202
    dependency group:'org.springframework.security', name: 'spring-security-rsa', version: '1.1.5'
    dependency group: 'org.bouncycastle', name: 'bcpkix-jdk18on', version: '1.82'
    dependency group: 'org.camunda.connect', name: 'camunda-connect-connectors-all', version: '7.24.0'

    dependencySet(group: 'commons-fileupload', version: '1.6.0') {
      entry 'commons-fileupload'
    }
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'}
}

def versions = [
  camunda           : '7.23.0',
  jackson           : '2.16.0',
  lombok            : '1.18.42',
  pact              : '4.1.34',
  springBoot        : springBoot.class.package.implementationVersion
]



dependencies {
// uncomment for local version
// implementation group: 'com.github.hmcts', name: 'ccd-config-generator', version: 'DEV-SNAPSHOT'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
  implementation (group: 'com.github.hmcts', name: 'befta-fw', version: '9.2.4') {
    exclude group: 'org.apache.commons', module: 'commons-compress'
    exclude group: 'com.google.guava', module: 'guava'
    exclude group: 'org.apache.poi', module: 'poi-ooxml'
    exclude group: 'org.apache.poi', module: 'poi'
  }

  implementation 'io.swagger:swagger-annotations:1.6.16'
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.29'
  implementation group: 'ch.qos.logback', name: 'logback-core', version: '1.5.29'
  implementation group: 'com.github.hmcts', name: 'core-case-data-store-client', version: '5.2.0'
  implementation group: 'com.github.hmcts', name:'ccd-case-document-am-client', version: '1.59.2'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '3.0.5'
  implementation group: 'com.github.hmcts', name: 'pdf-service-client', version: '8.0.2'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.9'
  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.25.3'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: '2.25.3'
  implementation group: 'org.elasticsearch', name: 'elasticsearch' , version: '7.17.29'
  implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.8.14'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '2.0.12'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '4.3.0'
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.3.0'
  implementation group: 'org.springframework.data', name: 'spring-data-commons', version: '3.5.8'
  implementation group: 'uk.gov.service.notify', name: 'notifications-java-client', version: '5.2.1-RELEASE'
  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '7.10.2'
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.5.0'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.20.0'
  implementation group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.2.3'
  implementation 'org.camunda.bpm:camunda-external-task-client:7.24.0'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.10'
  implementation 'org.flywaydb:flyway-database-postgresql:11.3.1'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.10', classifier: 'all'

  testImplementation group: 'jakarta.enterprise', name: 'jakarta.enterprise.cdi-api', version: '4.1.0'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    exclude(module: 'commons-logging')
    exclude(module: 'android-json')
  }

  testImplementation group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
  testImplementation group: 'com.github.stefanbirkner', name: 'system-lambda', version: '1.2.1'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.27.7'
  testImplementation group: 'org.assertj', name: 'assertj-guava', version: '3.27.7'
  testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-stub-runner', version: '4.3.0'
  testImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
  testImplementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.6'
  testImplementation group: 'io.rest-assured', name: 'rest-assured'
  testImplementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.6'
  testImplementation group: 'com.microsoft.playwright', name: 'playwright', version: '1.56.0'
  testImplementation group: 'org.camunda.bpm.dmn', name: 'camunda-engine-dmn', version: "${versions.camunda}"
  testImplementation group: 'org.camunda.bpm', name: 'camunda-engine-plugin-spin', version: "${versions.camunda}"
  testImplementation group: 'org.camunda.bpm.assert', name: 'camunda-bpm-assert', version: '15.0.0'
  testImplementation group: 'org.camunda.bpm', name: 'camunda-engine', version: '7.24.0'

  testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  integrationTestImplementation sourceSets.main.runtimeClasspath
  integrationTestImplementation sourceSets.test.runtimeClasspath
  integrationTestImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: '1.21.3'
  integrationTestImplementation group: 'org.testcontainers', name: 'postgresql', version: '1.21.3'

  functionalTestImplementation sourceSets.main.runtimeClasspath
  functionalTestImplementation sourceSets.test.runtimeClasspath
  functionalTestImplementation 'com.h2database:h2'
  configGeneration 'com.h2database:h2'

  functionalTestImplementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'
  functionalTestImplementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '5.3.3'

  functionalTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
  functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.6.19'

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath

  // Provides fast-reload of just the sptribs service.
  cftlibImplementation 'org.springframework.boot:spring-boot-devtools'
  cftlibTestImplementation 'com.microsoft.playwright:playwright:1.56.0'
  cftlibTestImplementation 'org.junit-pioneer:junit-pioneer:2.3.0'
  cftlibTestImplementation 'io.github.artsok:rerunner-jupiter:2.1.6'
}

mainClassName = 'uk.gov.hmcts.sptribs.CaseApiApplication'

def loadEnvSecretsTask = tasks.register('loadEnvSecrets') {
  doLast {
    if (!project.file('./.aat-env').exists()) {
      new ByteArrayOutputStream().withStream { os ->
        exec {
          commandLine 'az', 'keyvault', 'secret', 'show', '--vault-name', 'sptribs-aat', '-o', 'tsv', '--query', 'value', '--name', 'sptribs-case-api-dot-env'
          standardOutput = os
        }
        project.file('./.aat-env').write(new String(os.toString().replace('\n', '').decodeBase64(), java.nio.charset.StandardCharsets.UTF_8))
      }
    }
  }
}

tasks.register('reloadEnvSecrets') {
  dependsOn loadEnvSecretsTask
  doFirst {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').delete()
    }
  }
}

bootRun {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

bootJar {
  getArchiveFileName().set(provider {
    'sptribs-case-api.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

functional {
  doFirst() {
    if (project.file('./.aat-env').exists()) {
      project.file('./.aat-env').readLines().each() {
        def index = it.indexOf("=")
        def key = it.substring(0, index)
        def value = it.substring(index + 1)
        environment key, value
      }
    }
  }
}

generateTypeScript {
  jsonLibrary = 'jackson2'
  classPatterns = [
    'uk.gov.hmcts.sptribs.**.model.*',
    'uk.gov.hmcts.ccd.sdk.type.*',
  ]
  outputFile = 'build/ts/index.ts'
  outputKind = 'module'
  outputFileType = 'implementationFile'
  mapEnum = 'asEnum'
  mapDate = 'asString'
  group = 'ccd tasks'
  excludeClasses = [
    'uk.gov.hmcts.sptribs.caseworker.model.ComponentLauncher',
    'uk.gov.hmcts.ccd.sdk.type.Document',
    'uk.gov.hmcts.reform.ccd.client.model.Classification'
  ]

  doLast {
    exec {
      commandLine 'yarn', 'prepublishOnly'
    }
  }
}

ccd {
  configDir = file('build/definitions')
}

tasks.withType(CftlibExec).configureEach {
  environment 'RSE_LIB_ADDITIONAL_DATABASES', 'sptribs'
  environment 'CCD_DECENTRALISED_CASE-TYPE-SERVICE-URLS_CriminalInjuriesCompensation', 'http://localhost:4013'
  environment 'SPRING_JMS_SERVICEBUS_ENABLED', 'false'
}

bootWithCCD {
    environment 'XUI_JURISDICTIONS', 'ST_CIC'
    environment 'CASE_DOCUMENT_S2S_AUTHORISED_SERVICES', 'ccd_case_document_am_api,ccd_gw,xui_webapp,ccd_data,bulk_scan_processor,em_npa_app,dg_docassembly_api,em_stitching_api,em_ccd_orchestrator,bulk_scan_orchestrator,sptribs_case_api,sptribs_frontend'
    environment 'PDF_API_URL', 'http://rpe-pdf-service-aat.service.core-compute-aat.internal'
    environment 'SPRING_JMS_SERVICEBUS_ENABLED', 'false'
    group = 'ccd tasks'
    dependsOn loadEnvSecretsTask

    doFirst() {
        project.file('./.aat-env').readLines().each() {
            def index = it.indexOf("=")
            def key = it.substring(0, index)
            def value = it.substring(index + 1)
            environment key, value
        }
    }
}

assemble.dependsOn(dumpCCDDefinitions)

cftlibTest {
  // IDAM simulator will be started on port 5000,
  // S2S simulator on port 8489
  authMode = uk.gov.hmcts.rse.AuthMode.Local
  group = 'verification'

  systemProperties([
    // Configuration parameters to execute top-level classes in parallel but methods in same thread
    'junit.jupiter.execution.parallel.enabled': 'true',
    'junit.jupiter.execution.parallel.mode.default': 'same_thread',
    'junit.jupiter.execution.parallel.mode.classes.default': 'concurrent',
  ])
  environment 'XUI_JURISDICTIONS', 'ST_CIC'
  environment 'PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD', '1'
  environment 'RD_PROFESSIONAL_API_URL', 'http://host.docker.internal:8765'
  environment 'DOC_ASSEMBLY_URL', 'http://localhost:8765'
  environment 'FEE_API_URL', 'http://localhost:8765'
  environment 'CCD_DOCUMENT_URL_PATTERN', '.*'
  environment 'DM_STORE_BASE_URL', 'http://localhost:8765'
  environment 'CASE_DOCUMENT_AM_API_ATTACH_DOCUMENT_ENABLED', 'false'
  environment 'CASE_DATA_STORE_BASEURL', 'http://localhost:4452'
  environment 'ROLE_ASSIGNMENT_URL', 'http://localhost:4096'
  environment 'CCD_S2S_AUTHORISED_SERVICES_CASE_USER_ROLES', 'sptribs_case_api'
  environment 'DATA_STORE_S2S_AUTHORISED_SERVICES', 'ccd_gw,fpl_case_service,ccd_data,ccd_ps,sptribs_frontend,xui_webapp,sptribs_case_api,ccd_case_document_am_api,am_role_assignment_service'
}

// exclude the logback binding is coming in via cftlib
configurations.cftlibTestImplementation {
  exclude group: 'ch.qos.logback', module: 'logback-classic'
}

tasks.register('installChromium', JavaExec) {
  // group = "Execution"
  // description = "Run the main class with JavaExecTask"
  classpath = sourceSets.cftlibTest.runtimeClasspath
  mainClass.set('com.microsoft.playwright.CLI')
  args = ['install', 'chromium']
}

tasks.cftlibTest.dependsOn(installChromium)

// exclude the logback binding is coming in via cftlib
configurations.testImplementation {
  exclude group: 'ch.qos.logback', module: 'logback-classic'
}

tasks.named("processContractTestResources") {
  duplicatesStrategy = 'include'
}

project.ext {
  pactVersion = getCheckedOutGitCommitHash()
}

pact {
  publish {
    pactDirectory = 'pacts'
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'https://pact-broker.platform.hmcts.net/'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: getCheckedOutGitCommitHash(), 'latest']
    version = project.pactVersion
  }
}

tasks.register('runAndPublishConsumerPactTests', Test) {
  logger.lifecycle("Runs consumer pact tests")
  systemProperty 'pact.rootDir', "pacts"
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --short HEAD'.execute().text.trim()
}

runAndPublishConsumerPactTests.finalizedBy pactPublish
